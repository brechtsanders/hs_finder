CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(hs_finder)

# parameters
OPTION(BUILD_STATIC "Build static libraries" ON)
OPTION(BUILD_SHARED "Build shared libraries" ON)
OPTION(BUILD_TOOLS "Build tools" ON)
SET(HYPERSCAN_DIR "" CACHE PATH "Path to the Hyperscan library")

# conditions
IF(NOT BUILD_STATIC AND NOT BUILD_SHARED)
  MESSAGE(FATAL_ERROR "Cannot build with both BUILD_STATIC and BUILD_SHARED disabled")
ENDIF()

# dependancies
SET(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/CMake" ${CMAKE_MODULE_PATH})
FIND_PACKAGE(Hyperscan REQUIRED)

# Doxygen
FIND_PACKAGE(Doxygen)
OPTION(BUILD_DOCUMENTATION "Create and install API documentation (requires Doxygen)" ${DOXYGEN_FOUND})

# build parameters
SET(CMAKE_C_FLAGS "-Wall")

INCLUDE_DIRECTORIES(include)
INCLUDE_DIRECTORIES(${HYPERSCAN_INCLUDE_DIRS})

# build definitions
SET(ALLTARGETS)
SET(LINKTYPES)
IF(BUILD_STATIC)
  LIST(APPEND LINKTYPES "STATIC")
ENDIF()
IF(BUILD_SHARED)
  LIST(APPEND LINKTYPES "SHARED")
ENDIF()

FOREACH(LINKTYPE ${LINKTYPES})
  ADD_LIBRARY(hs_finder_${LINKTYPE} ${LINKTYPE} lib/hs_finder.c lib/search_data_buffer.c lib/hyperscan_expr_list.c)
  IF(LINKTYPE STREQUAL "SHARED")
    SET_TARGET_PROPERTIES(hs_finder_${LINKTYPE} PROPERTIES DEFINE_SYMBOL "BUILD_HS_FINDER_DLL")
  ENDIF()
  SET_TARGET_PROPERTIES(hs_finder_${LINKTYPE} PROPERTIES COMPILE_DEFINITIONS "${LINKTYPE}")
  SET_TARGET_PROPERTIES(hs_finder_${LINKTYPE} PROPERTIES OUTPUT_NAME hs_finder)
  TARGET_INCLUDE_DIRECTORIES(hs_finder_${LINKTYPE} PRIVATE lib)
  TARGET_LINK_LIBRARIES(hs_finder_${LINKTYPE} ${HYPERSCAN_LIBRARIES})
  SET(ALLTARGETS ${ALLTARGETS} hs_finder_${LINKTYPE})

  SET(EXELINKTYPE ${LINKTYPE})
ENDFOREACH()

IF(BUILD_TOOLS)
  ADD_EXECUTABLE(hs_finder_count src/hs_finder_count.c)
  TARGET_LINK_LIBRARIES(hs_finder_count hs_finder_${EXELINKTYPE})
  LIST(APPEND ALLTARGETS hs_finder_count)
  ADD_EXECUTABLE(hs_finder_replace src/hs_finder_replace.c)
  TARGET_LINK_LIBRARIES(hs_finder_replace hs_finder_${EXELINKTYPE})
  LIST(APPEND ALLTARGETS hs_finder_replace)
ENDIF()

IF(BUILD_DOCUMENTATION)
  IF(NOT DOXYGEN_FOUND)
    MESSAGE(FATAL_ERROR "Doxygen is needed to build the documentation.")
  ENDIF()
  ADD_CUSTOM_TARGET(doc ALL
    COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/doc/Doxyfile
    #WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM
  )
  INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/build/doc/man
    DESTINATION .
  )
  #INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/build/doc/html
  #  DESTINATION share/doc
  #)
ENDIF()

# installation specifications
INSTALL(TARGETS ${ALLTARGETS}
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)
INSTALL(DIRECTORY include/
  DESTINATION include 
  FILES_MATCHING PATTERN "hs_finder*.h"
)
